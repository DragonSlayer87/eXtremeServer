//
// Project: GoldMod Collect
// File Name: PlayerOwnage.cfg
// Date: 2013/2016
// Author: degrave/BudSpencer
//
// Purpose: send "killing spree" messages if a player reaches a specified number of frags without deaths/suicides
//

	
spree_onfrag = [
	fragnum = (getvar $arg1 nodie)
	fragnum = (+ $fragnum 1)
	setvar $arg1 nodie $fragnum

	spreecmd = (format "killspree_%1" $fragnum)
	if (identexists $spreecmd) [
		say (format (getalias $spreecmd) (getname $arg1) $arg1 $fragnum)
	]
]

spree_reset = [ setvar $arg1 nodie 0 ]

spree_resetall = [
	all = (clientlist)
	looplist cn $all [ spree_reset $cn ]
]

addhandler onfrag spree_onfrag
addhandler ondeath spree_reset
addhandler onsuicide spree_reset
addhandler onmapchange spree_resetall

killspree_5 = "Player ^f3%1(%2) ^f7is on a ^f4KILLING SPREE^f7!!! (^f4%3 ^f7frags^f7)"
killspree_10 = "Player ^f3%1(%2) ^f7is on a ^f4RAMPAGE^f7!!! (^f4%3 ^f7frags^f7)"
killspree_15 = "Player ^f3%1(%2) ^f7is on a ^f4DOMINATING^f7!!! (^f4%3 ^f7frags^f7)"
killspree_20 = "Player ^f3%1(%2) ^f7is on a ^f4UNSTOPPABLE^f7!!! (^f4%3 ^f7frags^f7)"
killspree_30 = "Player ^f3%1(%2) ^f7is ^f4LIKE A BOZZ^f7!!! (^f4%3 ^f7frags^f7)"
killspree_40 = "Player ^f3%1(%2) ^f7is on a ^f4UNBELIEVABLE^f7!!! (^f4%3 ^f7frags^f7)"
killspree_50 = "Player ^f3%1(%2) ^f7is on a ^f4GODLIKE^f7!!! (^f4%3 ^f7frags^f7)"
killspree_65 = "Player ^f3%1(%2) ^f7is on a ^f4KING OF CUBE 2^f7!!! (^f4%3 ^f7frags^f7)"
killspree_100 = "Player ^f3%1(%2) ^f7has scored freaky ^f4100 FRAGS^f7 without any deaths!!!"

	
	
