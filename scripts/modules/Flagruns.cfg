
///////////
// Show flagrun messages in the game
// @group server
// @min 0
// @max 1
// @type int
defaultvalue "flagrun_show_messages" 1

///////////
// Minimal time in milliseconds for flagrun. If flag was scored in a time less than this, there is high probability of cheating, so script kicks the player 
// @group server
// @min 0
// @type int
// @max MAX_INT
defaultvalue "flagrun_minmillis" 1000 // 1 second

///////////
// Which database should server use to store flagrun results. Empty value = do not use database.
// @group server
defaultvalue "flagrun_db" 0


// flagrun table
defaultvalue "flagrun_table" "flagrun"


flagkeeper_good_starttime = -1
flagkeeper_evil_starttime = -1

// best score time
// flugrun_best_5_reissen = "9602 |RB|degrave"




//************************************************
// Check if DB table exists and has proper columns
if $flagrun_db [
	sqlite_create_table = "CREATE TABLE `:0` (mode UNSIGNED TINYINT, map VARCHAR(40), name VARCHAR(40), time UNSIGNED INTEGER, unique(mode, map))"
	mysql_create_table = "CREATE TABLE IF NOT EXISTS `:0` (`mode` tinyint(3) unsigned NOT NULL, `map` varchar(40), `name` varchar(40), `time` int(10) unsigned NOT NULL, PRIMARY KEY (`mode`,`map`)) ENGINE=InnoDB DEFAULT CHARSET=utf8"
	db_check_table "flagrun" $flagrun_db $flagrun_table "mode map name time" $sqlite_create_table $mysql_create_table
]




// flagrun_addresult $run(ms) $name(str)
flagrun_addresult = [
	flagrun_modestring = (concatword "flagrun_best_" (getmode) "_" (getmap))
	flagrun_modescore = (getalias $flagrun_modestring)

	// check for not defined time and new record
	if (|| (=s $flagrun_modescore "") (< $arg1 (at $flagrun_modescore 0))) [
		alias $flagrun_modestring (listconcat $arg1 $arg2)
		if $flagrun_db [ flagrun_db_addresult $arg1 $arg2 ]
	]
]

flagrun_bestresult = [
	flagrun_modestring = (concatword "flagrun_best_" (getmode) "_" (getmap))
	result (getalias $flagrun_modestring)
]

flagrun_ontakeflag = [
	//if flag was taken from base
	if (= $arg3 1) [
		if (=s $arg2 "evil") [
			flagkeeper_good_starttime = $totalmillis
		] [
			flagkeeper_evil_starttime = $totalmillis
		]
	]
]
	
flagrun_ondropflag = [
	if (=s $arg2 "evil") [
		flagkeeper_good_starttime = 0
	] [
		flagkeeper_evil_starttime = 0
	]
]

flagrun_onscoreflag = [
	flagkeeperstr = (concatword "flagkeeper_" $arg2 "_starttime")
	flagkeeper_starttime = (getalias $flagkeeperstr)
	// if flag taken from base
	isctf = (!= (indexof $CTFMODES (modetostr (getmode))) -1)
	if (&& (!= $flagkeeper_starttime 0) $isctf) [
		flagrun_millis = (abs (- $totalmillis $flagkeeper_starttime))

		// check for teleport hack
		if (>= $flagrun_millis $flagrun_minmillis) [
			// teleport hack not detected, add result to table and check for new record
			flagrun_addresult $flagrun_millis (getname $arg1)
			flagrun_best = (flagrun_bestresult)
			flagrun_msg = (format "Player ^f3%1(%2) ^f7scored the flag in ^f4%3 ^f7seconds^f7." (getname $arg1) $arg1 (divf $flagrun_millis 1000))
		
			// check for new record
			current = (listconcat $flagrun_millis (getname $arg1))
			if (=s $flagrun_best $current) [
				flagrun_msg = (format "%1 (^f7New Map ^f4Record^f7)" $flagrun_msg)
				flagrun_msg_irc = (format "^f7New Flagrun ^f3Record^f7: Mode: ^f4%1 ^f7/ Map: ^f4%2 ^f7/ Name: ^f4%3 ^f7/ Time: ^f4%4^f7 seconds^f7." (at $MODENAMES (getmode)) (getmap) (getname $arg1) (divf $flagrun_millis 1000))
				// show message
				if (identexists "ircsay") [
					ircsay $flagrun_msg_irc
				]
			] [
				flagrun_msg = (format "%1 ^f7(^f7Record Holder^f7: ^f4%2 ^f7with ^f4%3 ^f7seconds)." $flagrun_msg (listat $flagrun_best 1) (divf (listat $flagrun_best 0) 1000) )
			]
		
			if $flagrun_show_messages [
				say $flagrun_msg
			]
		] [
			// detected teleport hack or speedhack
			// show some warnings
			if ( ! $rugbymode ) [
                saytomaster (format "^f7[^f5ANTICHEAT^f7] Player ^f3%1^f7(Client Number: ^f4%2^f7) may use the ^f4Speed Hack^f7!" (getname $arg1) $arg1)
                saytoadmin (format "^f7[^f5ANTICHEAT^f7] Player ^f3%1^f7(Client Number: ^f4%2^f7 / IP-Address: ^f4%3^f7) may use the ^f4Speed Hack^f7!" (getname $arg1) ( getip $arg1 ) )
                ircsay (format "^f7[^f5ANTICHEAT^f7] Player ^f3%1^f7(Client Number: ^f4%2^f7) may use the ^f4Speed Hack^f7!" (getname $arg1) $arg1)
                log_cheater (format "^f7[^f5ANTICHEAT^f7] Player ^f3%1^f7(Client Number: ^f4%2^f7) may use the ^f4Speed Hack^f7!" (getname $arg1) $arg1)
            ]
		]
        if ( = $rugbymode 0 ) [
            // kick immediatly if flagrun time is less than 3000 ms
            if (<= $flagrun_millis 3000) [ kick $arg1 ]
		
            alias $flagkeeperstr 0
            ]
	]
]

flagrun_onreturnflag = [
	if (=s $arg2 "evil") [
		flagkeeper_good_dropped = 0
	] [
		flagkeeper_evil_dropped = 0
	]
]

flagrun_onresetflag = [
	if (=s $arg2 "evil") [
		flagkeeper_good_dropped = 0
	] [
		flagkeeper_evil_dropped = 0
	]
]

flagrun_onmapstart = [
	flagkeeper_good_dropped = 0
	flagkeeper_evil_dropped = 0
]



// database staff 
// int mode (uniq) | varchar[20] map (uniq) | varchar[20] name | int time


//load flagrun data from database
flagrun_db_loadtime = [
	db_init $flagrun_db
	echo (format "Loading flagrun results from database '%1'" $flagrun_db)
	// make query
	frun_query = (format "SELECT mode, map, name, time FROM %1" $flagrun_table)
	qh = (db_query $frun_query $flagrun_db)
	// check for errors
	if (= $qh -1) [
		db_error $flagrun_db
	] [
		// no errors
		// create aliases for records		
		while [row = (db_getrow $qh $flagrun_db); result (!=s $row "")] [
			// row format "17 wdcd |RB|degrave 3423"
			// create aliases
			frun_aname = (concatword "flagrun_best_" (listat $row 0) "_" (listat $row 1))
			
			frun_avalue = (listconcat (listat $row 3) (listat $row 2))
			log_info (format "Flagrun: loaded %1 %2 (%3 ms)" $frun_aname (listat $row 2) (listat $row 3))
			alias $frun_aname $frun_avalue
		]
		db_finalize $qh $flagrun_db
	]
]

// add result to database
flagrun_db_addresult = [
	// create table flagrun(mode UNSIGNED TINYINT, map VARCHAR(20), name VARCHAR(20), time UNSIGNED INTEGER, unique(mode, map));

	// variables for insert
	frun_mode = (getmode)
	frun_map = (getmap)
	frun_name = $arg2
	frun_time = $arg1	

	// make query
	
	
	db__uid = (db_get_dbuid $flagrun_db)
	
	cases (db_get_engine $flagrun_db) "sqlite3" [
		sqlite3_query $db__uid "BEGIN TRANSACTION"
		sqlite3_pquery $db__uid "INSERT OR REPLACE INTO `:0` (mode, map, name, time) VALUES (:1, ':2', ':3', ':4')" $flagrun_table $frun_mode $frun_map $frun_name $frun_time
		res = (sqlite3_query $db__uid "COMMIT")
	] "mysql" [ 
		res = (mysql_pquery $db__uid "REPLACE INTO `:0` (mode, map, name, time) VALUES (:1, ':2', ':3', ':4')" $flagrun_table $frun_mode $frun_map $frun_name $frun_time)
	] () [
		res = -1
	]
	
	// check for errors
	if (=s $res -1) [  
		db_error $flagrun_db
	]
	log_info (format "Added new result to flagrun database: Name: %1 / Time: %2" $frun_name $frun_time)
]



// load all records from database
if $flagrun_db [ flagrun_db_loadtime ]

addhandler ontakeflag flagrun_ontakeflag
addhandler ondropflag flagrun_ondropflag
addhandler onscoreflag flagrun_onscoreflag  

addhandler onreturnflag flagrun_onreturnflag
addhandler onresetflag flagrun_onresetflag

addhandler onmapstart flagrun_onmapstart




// shows best flagrun
cmd_flagrun = [
	if ( commandisdisabled "flagrun" ) [ pm $arg1 ( format $command_is_disabled_msg "flagrun" ) ] [
		if ( commandisdisabled "flagrun" ) [ pm $arg1 ( format $command_is_disabled_msg "flagrun" ) ] [
			current_mode = (getvalue "arg2" (at $MODENAMES (getmode)))
			current_map = (getvalue "arg3" (getmap))
		
			if (= (isinlist $FLAGMODES $current_mode) 0) [
				pm $arg1 (format "Sorry, this gamemode ^f3(%1) ^f7has no flags. Available modes (with flags) are: ^f4%2" $current_mode $FLAGMODES)
			] [
				frun_aname = (concatword "flagrun_best_" (indexof $MODENAMES $current_mode) "_" $current_map)
				if (identexists $frun_aname) [
					flagrun_best = (getalias $frun_aname)
					msg = (format "Flagrun record for ^f3%1 ^f7on ^f4%2^f7: Player ^f4%3 ^f7with ^f4%4 ^f7seconds." $current_mode $current_map (listat $flagrun_best 1) (divf (listat $flagrun_best 0) 1000) )
				] [
					msg = "Here aren't any ^f3flagrun records ^f7yet. Do a record and ^f4try again^f7."
				]
				pm $arg1 $msg
				
			]
		]
	]
]
