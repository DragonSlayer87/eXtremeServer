//
// File Name: cmd_votekick.cfg
// Date: 2016
// Author: BudSpencer 
//
// Purpose: vote to kick a specified player
//


votekick_list = ""
min_clients = 4

VoteToKick = [
	if ( commandisdisabled "votekick" ) [ pm $arg1 ( format $command_is_disabled_msg "votekick" ) ] [
		runner = $arg1 ; target = $arg2 ; cn2kick = ( getvar $target votes )
		limit = ( getvkmslimit ( numclients ) )
			if ( >= ( numclients ) $min_clients ) [ // only vote if >= 4 players are connected
				if ( = ( indexof $votekick_list ( getip $runner ) ) -1 ) [ // if not already voted
					if ( < $target 128 ) [ // no bots
						if ( != $target $runner ) [ // if runner not votes to kick himself
							if ( < ( getvar $target votes ) ( - $limit 1 ) ) [ // if limit isn't reached
								setvar $target votes ( + $cn2kick 1 )
								say ( format "Player ^f3%1(%2) ^f7voted to kick Player ^f4%3(%4)^f7. Votes: ^f4%5 ^f7of ^f4%6^f7 Votes." ( getname $runner ) $runner ( getname $target ) $target ( getvar $target votes ) $limit )
								votekick_list = ( listadd $votekick_list ( getip $runner ) )
							] [
								if ( >= ( getvar $target votes ) ( - $limit 1 ) ) [ 
									say ( format "Player ^f3%1(%2) ^f7voted to kick Player ^f4%3(%4)^f7. Limit of ^f4%5 ^f7is reached! Player ^f4%3(%4) ^f7get kicked now." ( getname @runner ) @runner ( getname @target ) @target @limit )
									asleep 50 [ kick $arg1 @target ( * $hour $votekick_kick_time ) "Victim of Votekick command" ] // $votekick_kick_time hours ban
								]
							]
						] [
							pm $runner ( format "You can't ^f3vote ^f7to kick ^f4yourself^f7." )
						]
					] [
						pm $runner ( format "You can't ^f3vote ^f7to kick a ^f4bot^f7." )
					]
				] [
					pm $runner ( format "You already ^f3voted ^f7to kick a ^f4player^f7." )
				]
			] [
				pm $runner ( format "Here aren't enough ^f3players ^f7for ^f4#votekick ^f7to work. You need at least ^f4%1 ^f7players^f7." $min_clients )
			]	
		runner = -1 ; target = -1 // protect variables from reading
	]
]

reset_vars___ = [ votekick_list = "" ]

addhandler "onimission" reset_vars___
registercommand "votekick" VoteToKick 1 "c" "votekick <cn> ^f3This command allows you to vote to kick a specified player."
		